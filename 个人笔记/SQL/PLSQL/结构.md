## 结构控制

#### if 结构

```sql
IF condition THEN -- then 不能省
statements; -- 当 condition 为 true 时，才会执行 statements
END IF;
```

```sql
declare
    v_result number(10,4); -- 定义变量的数据类型，长度十位，保留4位小数
    begin
    v_result:=sqrt(58+25*3+(19-9)**2); -- 给变量赋值
    if v_result > 10 then
    DBMS_OUTPUT.PUT_LINE('平方根='||v_result); -- v_result 结果大于10，执行6-7行的语句
    DBMS_OUTPUT.PUT_LINE('结果为true，输出结果');
    end if; -- 结果为大于10，为true，执行到这里结束
    DBMS_OUTPUT.PUT_LINE('结果为fase，不满足条件'); -- if的条件结果不大于10，直接执行此语句，结束运行
end;
```

#### if……else

- 二选一的结果，要么显示if后的内容，要么显示else后的内容

```sql
if condition then -- condition 结果为true 执行if 后的 statements
statements;
else -- condition 结果为 false 执行 else 后的 statements
statements;
end if;
```

```sql
declare
    n_result number(10);
    begin
    n_result := 30;
    if n_result >= 30 then
        DBMS_OUTPUT.PUT_LINE('结果大于30'); -- 变量大于等于30执行这个
    else
        DBMS_OUTPUT.PUT_LINE('结果小于30'); -- 变量小于30执行这个
    end if;
end;
```

#### if……elsif

- 执行所有结果为 `true` 的语句，结果为 `false` 的语句就跳过，执行下一条语句，直到遇到 `end if`

```sql
declare
    n_student number(10);
begin
    n_student := DBMS_RANDOM.VALUE(100, 300);
    if n_student >= 100 and n_student <= 200 then
        DBMS_OUTPUT.PUT_LINE('数值范围在100~200之间');
    elsif n_student >= 200 and n_student <= 250 then
        DBMS_OUTPUT.PUT_LINE('数值范围在200~250之间');
    elsif n_student >= 250 and n_student <= 300 then
        DBMS_OUTPUT.PUT_LINE('数值范围在250~300之间');
        DBMS_OUTPUT.PUT_LINE('数值是' || n_student);
    else
        DBMS_OUTPUT.PUT_LINE('所有条件都不符合');
    end if;
end ;
```

#### if 嵌套

```sql
declare
    v_product products%rowtype;
    begin
    select * into v_product from PRODUCTS where PRODUCT_ID = 228;
    if v_product.LIST_PRICE>=2000 then -- 第一次判断成立，执行后面的语句
        DBMS_OUTPUT.PUT_LINE('该产品价格偏高');
        if v_product.STANDARD_COST>=2000 then -- 判断成立，结束此次判断，跳到 elsif 语句
            DBMS_OUTPUT.PUT_LINE('大于2000，不缺货');
            else
            DBMS_OUTPUT.PUT_LINE('小于2000，缺货');
        end if;
        -- 不管上面的if语句是否成立，都会执行这一句，这里的判断不成立，结束此次判断，之后的 if 和 else  都不会执行
        elsif v_product.LIST_PRICE >= 1000 and v_product.LIST_PRICE<=1500 then 
        DBMS_OUTPUT.PUT_LINE('中等价格');
        if v_product.STANDARD_COST >= 2000 then
            DBMS_OUTPUT.PUT_LINE('大于2000，不缺货');
            else
            DBMS_OUTPUT.PUT_LINE('小于2000，缺货');
        end if;
        else
        DBMS_OUTPUT.PUT_LINE('该产品价格偏低');
        if v_product.STANDARD_COST>=500 then
            DBMS_OUTPUT.PUT_LINE('不缺货');
            else
            DBMS_OUTPUT.PUT_LINE('缺货');
        end if;
    end if;
end;
```

#### 简单的 case 语句

- 给出一个表达式，并把表达式的结果同提供的几个可预见的结果比较，比较成功， 则执行对应的语句序列

```sql
[<< tag_name>>] -- 标签，可选；如果添加了，在代码结束处也需要加上这个标签；使用该便签可以提高代码可读性
case case_operand -- case_operand 变量，通常是一个表达式
when when_operand then -- when_operand 对应的是 case_operand 的结果；case 语句中，包含一个或多个 when ... then 语句
statement -- 如果 case_operand 和 when_operand 的结果相同，执行此 statement
[
  when when_operand then
  statement
]...
[else statement[statement]]..; -- 当所有的when_operand 都不能和 case_operand 对应，就会执行这个 else 语句
-- 如果没有ELSE子句，系统会给出CASE_NOT_FOUND异常
end case[tag_name]
```

```sql
declare
    v_categoryid varchar2(12);
begin
    select CATEGORY_ID into v_categoryid from PRODUCTS where PRODUCT_ID = 228;
    case v_categoryid
        when '2' then DBMS_OUTPUT.PUT_LINE(v_categoryid || '对应fake'); -- 找到对应的则会直接结束 case ，不会执行之后的 when...then 
        when '3' then DBMS_OUTPUT.PUT_LINE(v_categoryid || '对应aa');
        when '4' then DBMS_OUTPUT.PUT_LINE(v_categoryid || '对应bb');
        when '1' then DBMS_OUTPUT.PUT_LINE(v_categoryid || '对应ironman');
        else DBMS_OUTPUT.PUT_LINE('没有找到对应的类型'); 
        end case;
end;
```



#### 搜索式 case 语句

- 搜索式的CASE语句会依次检查布尔值是否为TRUE，一旦为TRUE，那么它所在的WHEN子句会被执行，而且它后面的布尔表达式将不被考虑。如果所有的布尔表达式都不为TRUE，那么程序将转到ELSE子句，如果没有ELSE子句，系统会给出CASE_NOT_FOUND异常

```sql
[<<tag_name>>]
case
when boolean_expression then statement;
[when boolean_expression then statement;]...
[else statement[statement]]..
end case <tag_name>
```

```sql
declare
    v_productinfo number(10,2); -- 声明变量
    begin
    select LIST_PRICE into v_productinfo from PRODUCTS where PRODUCT_ID = 98; -- 给变量赋值
    case
        when v_productinfo > 3000 then
        DBMS_OUTPUT.PUT_LINE('高价产品，价格是：'|| v_productinfo);
        when v_productinfo >= 2000 and v_productinfo <= 3000 then
        DBMS_OUTPUT.PUT_LINE('中价产品，价格是：'|| v_productinfo); -- 遇到为 true 的，结束此 case
        when v_productinfo <= 1000 then
        DBMS_OUTPUT.PUT_LINE('低价产品，价格是：'|| v_productinfo);
        else
        DBMS_OUTPUT.PUT_LINE('都不符合，价格是：'||v_productinfo);
        end case;
end;
```
#### loop 循环

```sql
[<<tag_name>>]
loop
statement,,
end loop
<tag_name>
```

##### 基本的loop语句

- 需要和条件控制语句一起使用，否则会死循环
- 使用 if 与 exit 的组合来终止循环；exit 必须在循环体内，它可以使循环无条件终止，并且可以继续执行循环体外的语句；如果和 if 搭配使用，则满足某个条件才会跳出循环，继续向下执行

```sql
declare
    n_num number(8) := 1; -- 定义变量并赋值
begin
    <<basicloop>>
    loop -- 进入循环体
        DBMS_OUTPUT.PUT_LINE('当前变量的值是：' || n_num);
        n_num := n_num + 1; -- 先讲变量加一，
        if n_num > 5 then -- 2不大于5，不执行接下来的语句，返回循环体的第一行重新执行
            DBMS_OUTPUT.PUT_LINE('结束，当前变量的值是：' || n_num); -- 变量大于5时，执行此语句
            exit basicloop; -- 终止循环，后面跟的是标签，增强可读性
        end if;
    end loop;
    DBMS_OUTPUT.PUT_LINE('循环结束');
end ;
```

- Exit 默认终止退出当前循环，但如果使用标签，可以终止并退出指定的`loop`循环
- 使用 `exit...when`来结束循环，游标常用；当 `when`后面的结果为`true`时，`exit`则会被触发，终止退出指定的循环;
- `exit`后不加 `loop`标签，则表示终止退出当前循环，可以替换简单的 `if`语句

```sql
declare
    n_num number(8) := 1;
begin
    <<basicloop>>
    loop
        DBMS_OUTPUT.PUT_LINE('当前变量的值是：' || n_num);
        n_num := n_num + 1;
            exit basicloop when n_num > 5; 
    end loop;
    DBMS_OUTPUT.PUT_LINE('循环结束');
end ;
```

##### while...loop

- 此机构本身可以终止`loop`循环，当`while`后的语句为 `true`时，`loop` 和`end loop`之间的语句集将执行一次，然后再次判断 `while` 后的表达式是否为 `true`

```sql
<<tag_name>>
while boolean_expression
loop
statement
end loop tag_name;
```

```sql
declare
    n_num number(8) := 1;
begin
    DBMS_OUTPUT.PUT_LINE('当前变量的值是：' || n_num);
    <<while_loop>>
    while n_num < 20
        loop
            if mod(n_num, 3) = 0 then
                DBMS_OUTPUT.PUT_LINE('能被3整除的数有：' || n_num);
            end if;
            n_num := n_num + 1;
        end loop;
    DBMS_OUTPUT.PUT_LINE('退出，当前变量的值是：' || n_num);
    DBMS_OUTPUT.PUT_LINE('结束循环');
end ;
```

- `while...loop` 的特点是，先判断再执行，`loop` 里的循环体可能一次都不会执行，上述示例代码可以优化

```sql
declare
    n_num number(8) :=1;
    v_num boolean := true;
    begin
    while v_num
    loop
        if mod(n_num,3)=0 then
            DBMS_OUTPUT.PUT_LINE('能被三整除的数字有：'||n_num);
        end if;
        if n_num >=20 then
            v_num := false;
        end if;
        n_num := n_num+1;
        end loop;
end;
```
##### for...loop
- FOR...LOOP语句循环遍历指定范围内的整数。该范围被FOR和LOOP关键词封闭。当第一
次进入循环时，其循环范围会被确定，并且以后不会再次计算。每循环一次，其循环次数将会
增加1。
```sql
<<tag_name>>
for index_name in -- index_name：循环计数器，此变量可以得到当前的循环次数，但不能为它赋值
[reverse] -- 指定循环的方式。默认的循环方式是下标界到上标界，也就是从lower到upper，如果使用 reverse ，循环就会相反，从上标界到下标界 upper 到 lower
lower_bound..upper_bound -- 两者之间用 .. 连接
loop
statement...
end loop tag_name;
```
- FOR...LOOP循环当中的循环范围可以动态地获取。例如，示例中的下标界完全可以用数值型的变量替代。
- 当lower_bound和upper_bound相等时，循环中的语句只能执行一次。
```sql
declare  
    num number(8) := 0;  
begin  
    <<for_loop>>  
    for inx in 1..20  
        loop  
            num := num + inx;  
        end loop for_loop;  
    DBMS_OUTPUT.PUT_LINE('1到100之间整数和是：' || num);  
    DBMS_OUTPUT.PUT_LINE('loop 循环结束');  
end;
```
